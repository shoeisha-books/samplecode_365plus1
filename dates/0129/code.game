 1' renumber for GAME Ver0.04 2025/08/22 by skyriver
 2 "src address " S=? "dst address " D=?
 3 "line start " B=? "line step " E=?
 4 Z=&+1 TBL=Z+6 P=S IDX=0 C=D
 5 @
 6  TBL(IDX)=P:0)*256+P:1) IDX=IDX+1 ;=%(IDX/10)=0 "*"
 7  P=P+2 @ P=P+1 @=(P:-1)=0)
 8 @=(P:0)>=$F0)
 9 P=S IDX=0
10 @
11  NEW=E*IDX+B D:0)=NEW/256 D:1)=NEW D=D+2 P=P+2
12  / ?(5)=TBL(IDX) " :" ?(5)=NEW
13  ;=P:0)<>" " @ D:0)=P:0) D=D+1 P=P+1 @=(P:0)=0)
14  @
15   ;=(P:0)=""")*(P:1)=""")*(P:2)=""") D:0)=""" D=D+1 P=P+1
16   ;=(P:0)=""") @ D:0)=P:0) D=D+1 P=P+1 @=(P:0)=""")
17   ;=((P:0)="#")+(P:0)="!"))*(P:1)="=")*(P:2)<>"-") !=24
18   D:0)=P:0) D=D+1 P=P+1
19  @=(P:-1)=0)
20  IDX=IDX+1
21 @=(P:0)>=$F0)
22 D:0)=P:0) /"Renumbered(" ??=C "-" ??=D ")"
23 #=-1
24 D:0)=P:0) D:1)="=" D=D+2 P=P+2 LINE=0
25 @ LINE=LINE*10+(P:0)-"0") P=P+1 @=((P:0)<"0")+(P:0)>"9"))
26 " " $=D:-2) "=" ?=LINE " -> " F=-1 @ F=F+1 @=(TBL(F)>=LINE)
27 LINE=E*F+B X=Z-1 ?=LINE
28 @ X=X+1 LINE=LINE/10 X:0)=%(0)+"0" @=(LINE=0)
29 @ D:0)=X:0) D=D+1 X=X-1 @=(X<Z)
30 ]